;===============================================
; Rosetta.art
;
; RosettaCode helper wrapper
; for Arturo
;
; MIT License
; (c) 2025 Yanis Zafirópulos
;===============================================

import ./{../../mwiki.art/src/mwiki}!

;===========================================
; Type definitions
;===========================================

define :rcTask [
    init: constructor [title content]

    hasLanguage?: method [lang :string][
        ;; description: « check if task has implementation for given language
        ;; returns: :logical :null

        if null? \content -> return ø
        return contains? \content "=={{header|" ++ ~"|lang|}}=="
    ]
    
    string: method [][
        ~"Task: |\title|"
    ]
]

;===========================================
; Main Implementation
;===========================================

define :Rosetta [

    ;------------------------------
    ; Constructor
    ;------------------------------

    init: method [][
        \wiki: to :MW ["https://rosettacode.org/w/api.php"]!
        \loggedIn: false
    ]
    
    ;----------------------------------
    ; Core Methods (language-agnostic)
    ;----------------------------------
    
    login: method [username :string, password :string][
        ;; description: « authenticate with RosettaCode
        ;; returns: :logical

        \loggedIn: \wiki\login username password
        return \loggedIn
    ]
    
    task: method [title :string][
        ;; description: « retrieve task content
        ;; returns: :rcTask :null

        page: \wiki\page title
        if null? page -> return ø
        return to :rcTask @[title page\content]!
    ]
    
    addSolution: method [title :string, lang :string, code :string, summary :string :null][
        ;; description: « add or update solution for given language
        ;; returns: :logical

        unless \loggedIn -> return false
        
        page: \wiki\page title
        if null? page -> return false

        content: page\content
        sectionHeader: ~"==|lang|=="
        
        ; Check if language section exists
        content: (contains? content sectionHeader)? [
            ; Replace existing solution
            parts: split.by:sectionHeader content
            before: first parts
            after: split.by:"\n==" last parts
            
            newContent: before ++ sectionHeader ++ "\n" ++ code
            (1 < size after)?
                -> newContent ++ "\n==" ++ join.with:"\n==" slice after 1 (size after)-1
                -> newContent
        ][
            ; Add new section at end
            content ++ "\n\n" ++ sectionHeader ++ "\n" ++ code
        ]
        
        sum: summary ?? "Added " ++ lang ++ " solution"
        
        \wiki\editPage title content sum
    ]
    
    fetchCategory: method [category :string][
        ;; description: « fetch all members of a category with pagination
        ;; returns: :block

        results: []
        continue: ""
        
        while ø [
            params: #[
                action: "query"
                list: "categorymembers"
                cmtitle: ~"Category:|category|"
                cmlimit: "500"
                format: "json"
            ]
            
            unless empty? continue ->
                params\cmcontinue: continue

            response: \wiki\api.get params
            body: read.json response\body
            
            'results ++ map body\query\categorymembers 'page ->
                page\title
                
            (key? body 'continue)?
                -> continue: body\continue\cmcontinue
                -> break
        ]
        
        return results
    ]
    
    unimplemented: method [lang :string][
        ;; description: « find all tasks without implementation for given language
        ;; returns: :block

        allTasks: \fetchCategory "Programming_Tasks"
        langTasks: \fetchCategory lang
        
        return difference allTasks langTasks
    ]
    
    implemented: method [lang :string][
        ;; description: « get all tasks with implementations for given language
        ;; returns: :block

        return \fetchCategory lang
    ]
    
    draftTasks: method [limit :integer :null][
        ;; description: « get draft programming tasks
        ;; returns: :block

        lmt: limit ?? 50
        return \wiki\search.limit:lmt "incategory:\"Draft Programming Tasks\""
    ]

    ;------------------------------
    ; Magic
    ;------------------------------
    
    string: method [][
        status: \loggedIn ? -> "logged in" -> "not logged in"
        ~"RosettaCode (|status|)"
    ]
]