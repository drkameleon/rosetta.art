;===============================================
; Rosetta.art
;
; RosettaCode helper wrapper
; for Arturo
;
; MIT License
; (c) 2025 Yanis Zafirópulos
;===============================================

import ./{../../mwiki.art/src/mwiki}!

;===========================================
; Type definitions
;===========================================

define :rcSolution [
    init: constructor [language content]
    
    string: method [][
        ~"Solution: |\language| (|size \content| chars)"
    ]
]

define :rcTask [
    init: method [wiki, title][
        \wiki: wiki
        \title: title

        \_content: attr 'content
        \_draft?: null
        \_description: null
        \_categories: null
        \_solutions: null
    ]
    
    parseContent: method [][
        ;; description: « parse task content into structured data
        
        if null? \_content -> return false
        
        lines: split.lines \_content
        
        ; Extract categories
        \_categories: select lines 'line ->
            contains? line "[[Category:"
        map \_categories 'cat [
            parts: split.by:":" cat
            if 1 < size parts [
                clean: replace last parts "]]" ""
                clean
            ] else -> cat
        ]
        
        ; Check if draft
        \_draft?: some? \_categories 'cat ->
            contains? cat "Draft Programming Tasks"
        
        ; Extract description (everything before first ==header==)
        descLines: []
        loop lines 'line [
            if and? [prefix? line "=="] [suffix? line "=="] -> break
            'descLines ++ line
        ]
        \_description: strip join.with:"\n" descLines
        
        ; Extract solutions
        \_solutions: []
        currentLang: null
        currentContent: []
        
        loop lines 'line [
            switch and? [prefix? line "=={{header|"] [contains? line "}}=="] [
                ; Save previous solution
                if currentLang [
                    sol: to :rcSolution @[currentLang join.with:"\n" currentContent]!
                    \_solutions: \_solutions ++ sol
                ]
                
                ; Start new solution
                parts: split.by:"|" line
                if 1 < size parts [
                    currentLang: replace (first slice parts 1 (size parts)-1) "}}==" ""
                    currentContent: []
                ]
            ][
                if currentLang [
                    'currentContent ++ line
                ]
            ]
        ]
        
        ; last solution
        if currentLang [
            sol: to :rcSolution @[currentLang join.with:"\n" currentContent]!
            \_solutions: \_solutions ++ sol
        ]
        
        return true
    ]
    
    content: method [][
        ;; description: « get task content, loading from wiki if needed
        ;; returns: :string :null
        
        if \_content -> return \_content
        
        page: \wiki\page \title
        if null? page -> return ø
        
        \_content: page\content
        \parseContent
        
        return \_content
    ]
    
    draft?: method [][
        ;; description: « check if task is a draft
        ;; returns: :logical :null
        
        if \_draft? -> return \_draft?
        
        ; Force content load which will parse and set \_draft?
        discard \content

        return \_draft?
    ]
    
    description: method [][
        ;; description: « get task description
        ;; returns: :string :null
        
        if \_description -> return \_description

        ; Force content load which will parse and set \_description
        discard \content

        return \_description
    ]
    
    categories: method [][
        ;; description: « get task categories
        ;; returns: :block :null
        
        if \_categories -> return \_categories

        ; Force content load which will parse and set \_categories
        discard \content

        return \_categories
    ]
    
    solutions: method [][
        ;; description: « get all language solutions
        ;; returns: :block :null
        
        if \_solutions -> return \_solutions

        ; Force content load which will parse and set \_solutions
        discard \content

        return \_solutions
    ]
    
    solution: method [lang :string][
        ;; description: « get solution for specific language
        ;; returns: :rcSolution :null
        
        sols: \solutions
        if null? sols -> return ø
        
        return select.first sols 'sol -> sol\language = lang
    ]

    hasLanguage?: method [lang :string][
        ;; description: « check if task has implementation for given language
        ;; returns: :logical :null

        cnt: \content
        if null? cnt -> return ø
        return contains? cnt "=={{header|" ++ ~"|lang|}}=="
    ]
    
    string: method [][
        ~"Task: |\title|"
    ]
]

;===========================================
; Main Implementation
;===========================================

define :Rosetta [

    ;------------------------------
    ; Constructor
    ;------------------------------

    init: method [][
        \wiki: to :MW ["https://rosettacode.org/w/api.php"]!
        \loggedIn: false
    ]
    
    ;----------------------------------
    ; Methods
    ;----------------------------------
    
    login: method [username :string, password :string][
        ;; description: « authenticate with RosettaCode
        ;; returns: :logical

        \loggedIn: \wiki\login username password
        return \loggedIn
    ]
    
    task: method [title :string][
        ;; description: « retrieve task content
        ;; returns: :rcTask :null

        page: \wiki\page title
        if null? page -> return ø
        
        return to :rcTask.content: page\content @[\wiki title]!
    ]
    
    addSolution: method [title :string, lang :string, code :string, summary :string :null][
        ;; description: « add or update solution for given language
        ;; returns: :logical

        unless \loggedIn -> return false
        
        page: \wiki\page title
        if null? page -> return false

        content: page\content
        sectionHeader: ~"==|lang|=="
        
        ; Check if language section exists
        content: (contains? content sectionHeader)? [
            ; Replace existing solution
            parts: split.by:sectionHeader content
            before: first parts
            after: split.by:"\n==" last parts
            
            newContent: before ++ sectionHeader ++ "\n" ++ code
            (1 < size after)?
                -> newContent ++ "\n==" ++ join.with:"\n==" slice after 1 (size after)-1
                -> newContent
        ][
            ; Add new section at end
            content ++ "\n\n" ++ sectionHeader ++ "\n" ++ code
        ]
        
        sum: summary ?? "Added " ++ lang ++ " solution"
        
        \wiki\editPage title content sum
    ]
    
    unimplemented: method [lang :string][
        ;; description: « find all tasks without implementation for given language
        ;; returns: :block
        
        allTasks: \wiki\category "Programming_Tasks"
        langTasks: \wiki\category lang
        
        if null? allTasks -> return []
        if null? langTasks -> return []

        cleanTitles: filter difference allTasks\members langTasks\members =>
            [prefix? & "Category:"]
        
        return map cleanTitles 'title ->
            to :rcTask @[\wiki title]!
    ]
    
    implemented: method [lang :string][
        ;; description: « get all tasks with implementations for given language
        ;; returns: :block

        langTasks: \wiki\category lang
        if null? langTasks -> return []
        
        cleanTitles: filter langTasks\members =>
            [prefix? & "Category:"]

        return map cleanTitles 'title ->
            to :rcTask @[\wiki title]!
    ]
    
    draftTasks: method [limit :integer :null][
        ;; description: « get draft programming tasks
        ;; returns: :block

        lmt: limit ?? 50
        titles: \wiki\search.limit:lmt "incategory:\"Draft Programming Tasks\""
        
        return map titles 'title -> to :rcTask @[\wiki title]!
    ]

    ;------------------------------
    ; Magic
    ;------------------------------
    
    string: method [][
        status: \loggedIn ? -> "logged in" -> "not logged in"
        ~"RosettaCode (|status|)"
    ]
]